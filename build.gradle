//импортируем зависимости которые нужны для работы данного файла gradle
plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.6'
}

//применяем ко всем модулям включая этот
allprojects {
    group = 'org.elena'
    version = '0.0.1-SNAPSHOT'

    repositories {
//        mavenLocal()
        mavenCentral()
    }
}

//применяем ко всем дочерним модулям
subprojects {
    //применяем эти плагины ко всем дочерним проектам
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    //если прописано, то не нужно прописывать sourceCompatibility, targetCompatibility
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(23)
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'
    }
    //таким образом мы даем возможность имплементить без указания версии в дочерние депенденси
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.0"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
